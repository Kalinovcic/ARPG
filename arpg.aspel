import "imports.aspel";

framesPerSecond: int32 = 0;

window: glfw3.GLFWwindow^;
windowFullscreen: bool;

errorCallback :: func void(error: int32, desc: int8^)
{
    reportString(nullstrToString(desc));
    asl.exit(1);
}

keyCallback :: func void(window: glfw3.GLFWwindow^, key: int32, scancode: int32,
                         action: int32, mods: int32)
{
    if action != glfw3.GLFW_REPEAT
    {
        press := action == glfw3.GLFW_PRESS;
        shift := (mods & glfw3.GLFW_MOD_SHIFT) != 0;
        handleKeyInput(key, press, shift);
    }
}

focusCallback :: func void(window: glfw3.GLFWwindow^, focused: int32)
{
    focus := focused == gl.GL_TRUE;
    handleFocusInput(focus);
}

mouseButtonCallback :: func void(window: glfw3.GLFWwindow^, button: int32,
                                 action: int32, mods: int32)
{
    press := action == glfw3.GLFW_PRESS;
    shift := (mods & glfw3.GLFW_MOD_SHIFT) != 0;
    handleMouseInput(button, press, shift);
}

mouseMotionCallback :: func void(window: glfw3.GLFWwindow^, xpos: float64, ypos: float64)
{
    handleMotionInput(cast(float32) xpos, cast(float32) ypos);
}

resizeCallback :: func void(window: glfw3.GLFWwindow^, width: int32, height: int32)
{
    updateCameraViewport();
}

openWindow :: func void()
{
    glfw3.glfwSetErrorCallback(errorCallback);

    if glfw3.glfwInit() != gl.GL_TRUE
    {
        reportString("Failed to init GLFW.\n");
        asl.exit(1);
    }

    windowFullscreen = false;

    window = glfw3.glfwCreateWindow(800, 600, "ARPG\0", null, null);
    if window == 0
    {
        reportString("Failed to open GLFW window.\n");
        glfw3.glfwTerminate();
        asl.exit(1);
    }

    setupWindow();
}

toggleFullscreen :: func void()
{
    windowFullscreen = !windowFullscreen;

    newWindow: glfw3.GLFWwindow^;
    if windowFullscreen
    {
        /*
        // borderless fullscreen
        primaryMonitor := glfw3.glfwGetPrimaryMonitor();
        mode := glfw3.glfwGetVideoMode(primaryMonitor);
        glfw3.glfwWindowHint(glfw3.GLFW_RED_BITS, mode.redBits);
        glfw3.glfwWindowHint(glfw3.GLFW_GREEN_BITS, mode.greenBits);
        glfw3.glfwWindowHint(glfw3.GLFW_BLUE_BITS, mode.blueBits);
        glfw3.glfwWindowHint(glfw3.GLFW_REFRESH_RATE, mode.refreshRate);
        newWindow = glfw3.glfwCreateWindow(mode.width, mode.height, "ARPG\0", primaryMonitor, window);
        */

        primaryMonitor := glfw3.glfwGetPrimaryMonitor();
        newWindow = glfw3.glfwCreateWindow(800, 600, "ARPG\0", primaryMonitor, window);
    }
    else
    {
        newWindow = glfw3.glfwCreateWindow(800, 600, "ARPG\0", null, window);
    }

    if newWindow == 0
    {
        reportString("Failed to open GLFW window.\n");
        glfw3.glfwTerminate();
        asl.exit(1);
    }

    glfw3.glfwDestroyWindow(window);
    window = newWindow;

    setupWindow();
    clearInputState();
}

setupWindow :: func void()
{
    glfw3.glfwMakeContextCurrent(window);
    glfw3.glfwSwapInterval(1);
    glfw3.glfwSetKeyCallback(window, keyCallback);
    glfw3.glfwSetCursorPosCallback(window, mouseMotionCallback);
    glfw3.glfwSetMouseButtonCallback(window, mouseButtonCallback);
    glfw3.glfwSetWindowFocusCallback(window, focusCallback);
    glfw3.glfwSetWindowSizeCallback(window, resizeCallback);

    initGL();
}

closeWindow :: func void()
{
    glfw3.glfwDestroyWindow(window);
    glfw3.glfwTerminate();
}

initGL :: func void()
{
    gl.glClearColor(1.0, 0.0, 1.0, 1.0);

    gl.glEnable(gl.GL_TEXTURE_2D);
    gl.glEnable(gl.GL_BLEND);
    gl.glBlendFunc(gl.GL_SRC_ALPHA, gl.GL_ONE_MINUS_SRC_ALPHA);
}

updateCameraViewport :: func void()
{
    bufferWidth: int32;
    bufferHeight: int32;
    glfw3.glfwGetFramebufferSize(window, &bufferWidth, &bufferHeight);
    setCameraViewport(bufferWidth, bufferHeight);
}

renderLoop :: func void()
{
    gameInit();
    updateCameraViewport();

    frames: int32 = 0;
    unprocessedTime: uint64 = 0;
    previousTime: uint64 = asl.timenanos();

    ticksPerSecond :: 60;
    tickTime :: cast(uint64)(1000000000.0 / ticksPerSecond);

    tickCount: int32 = 0;
    ticked: bool = false;

    while glfw3.glfwWindowShouldClose(window) != gl.GL_TRUE
    {
        currentTime: uint64 = asl.timenanos();
        deltaTime := currentTime - previousTime;
        previousTime = currentTime;

        unprocessedTime = unprocessedTime + deltaTime;
        while unprocessedTime >= tickTime
        {
            processInput();
            gameTick();
            updateInput();

            unprocessedTime = unprocessedTime - tickTime;
            tickCount = tickCount + 1;
            ticked = true;

            if tickCount % 60 == 0
            {
                framesPerSecond = frames;
                frames = 0;
            }
        }

        if ticked
        {
            gl.glClear(gl.GL_COLOR_BUFFER_BIT);
            gl.glMatrixMode(gl.GL_MODELVIEW);
            gl.glLoadIdentity();

            gameRender();

            glfw3.glfwSwapBuffers(window);
            glfw3.glfwPollEvents();

            frames = frames + 1;
            ticked = false;
        }
    }
}

main :: func void(args: asl.string[])
{
    openWindow();
    renderLoop();
    closeWindow();
}
